apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    mavenCentral()
    mavenRepo urls: "http://m2repo.spockframework.org/snapshots"
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.7.3'
    compile 'net.sf.opencsv:opencsv:2.1'
    testCompile "org.spockframework:spock-core:0.4-groovy-1.7"
    testCompile "cglib:cglib-nodep:2.2"
    testCompile "org.objenesis:objenesis:1.2"
}

version = '0.2-SNAPSHOT'
group = 'com.xlson.groovycsv'

sourceSets {
    main {
        groovy {
            srcDir 'src'
        }
    }
    test {
        groovy {
            srcDir 'test'
        }
    }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives groovydocJar
    archives sourcesJar
}

uploadArchives {
    // Checks if login information for the logon repo is set correctly.
    // Should be set in gradle.properties (check gradle.properties.example)
    if(project.hasProperty('repoUserName') && project.hasProperty('repoPassword')) {
        repositories.mavenDeployer {
            configuration = configurations.archives
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: repoUserName, password: repoPassword)
            }
            
            pom.project {
                name 'GroovyCSV'
                packaging 'jar' // not working
                description 'Library for parsing csv in Groovy'
                url 'http://github.com/xlson/groovycsv'
                inceptionYear '2010'
            
                scm {
                    url 'scm:git:git@github.com:xlson/groovycsv.git'
                    connection 'http://github.com/xlson/groovycsv'
                }
            
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            
                developers {
                    developer {
                        id 'xlson'
                        name 'Leonard Axelsson'
                        email 'leonard.axelsson@gmail.com'
                        url 'http://xlson.com/'
                        timezone '+1'
                    }
                }
            }
        }
    }

}

task downloadDeps << {
    def libDir = file('lib')
    ant.delete(dir: libDir)
    copy {
        from configurations.testRuntime
        into libDir
    }
}
